apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'jacoco'

jacoco {
    toolVersion = "$jacoco_version"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

android {
    compileSdkVersion rootProject.compileSdk

    defaultConfig {
        applicationId "com.mreigar.postapp"
        minSdkVersion rootProject.minSdk
        targetSdkVersion rootProject.targetSdk
        versionCode rootProject.versionCode
        versionName rootProject.versionName

        testInstrumentationRunner "com.mreigar.postapp.espresso.utils.PostAppTestRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        animationsDisabled true

        unitTests {
            includeAndroidResources = true
        }
    }

    signingConfigs {
        release {
            // Load keystore
            Properties properties = new Properties()
            if (System.getenv('KEY_PATH') != null) {
                properties.setProperty('STORE_FILE', System.getenv('KEY_PATH'))
                properties.setProperty('STORE_PASS', System.getenv('KEY_STORE_PASSWORD'))
                properties.setProperty('KEY_ALIAS', System.getenv('ALIAS'))
                properties.setProperty('KEY_PASS', System.getenv('KEY_PASSWORD'))
            } else {
                properties.load(new FileInputStream("$projectDir/keystores/prodtest/prodtest.properties"))
            }

            storeFile file(properties['STORE_FILE'])
            storePassword properties['STORE_PASS']
            keyAlias properties['KEY_ALIAS']
            keyPassword properties['KEY_PASS']
        }
    }

    buildTypes {
        debug {
            debuggable true
            testCoverageEnabled true
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation project(':presentation')
    implementation project(':network')
    implementation project(':local-storage')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation "org.koin:koin-android:$koin_version"
    testImplementation "org.koin:koin-test:$koin_version"

    implementation "com.google.android.material:material:$material_version"

    implementation "androidx.appcompat:appcompat:$androidx_compat_version"
    implementation "androidx.core:core-ktx:$androidx_core_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout_version"
    implementation "com.airbnb.android:lottie:$lottieVersion"

    implementation "com.github.bumptech.glide:glide:$glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"

    testImplementation 'junit:junit:4.12'
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"
    testImplementation "org.assertj:assertj-core:$assertj_version"
    testImplementation "org.robolectric:robolectric:$robolectric_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"
    androidTestImplementation "androidx.test:core:$androidx_test_version"

    androidTestImplementation "androidx.test.ext:junit:$test_junit_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"

    androidTestImplementation("com.schibsted.spain:barista:$barista_version") {
        exclude group: 'org.jetbrains.kotlin'
    }

}

android.sourceSets {
    test {
        java.srcDirs += "$projectDir/src/testShared"
    }

    androidTest {
        java.srcDirs += "$projectDir/src/testShared"
    }
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']

    def appClassTree = fileTree(dir: "../app/build/intermediates/javac/debug/classes/", excludes: fileFilter)
    def appKotlinClassTree = fileTree(dir: "../app/build/tmp/kotlin-classes/debug/", excludes: fileFilter)
    def appSrc = "../app/src/main/java"

    def presentationKotlinClassTree = fileTree(dir: "../presentation/build/classes/kotlin/", excludes: fileFilter)
    def presentationSrc = "../presentation/src/main/java"

    def domainKotlinClassTree = fileTree(dir: "../domain/build/classes/kotlin/", excludes: fileFilter)
    def domainSrc = "../domain/src/main/java"

    def dataKotlinClassTree = fileTree(dir: "../data/build/classes/kotlin/", excludes: fileFilter)
    def dataSrc = "../data/src/main/java"

    def localKotlinClassTree = fileTree(dir: "../local-storage/build/tmp/kotlin-classes/debug/", excludes: fileFilter)
    def localSrc = "../local-storage/src/main/java"

    def remoteKotlinClassTree = fileTree(dir: "../network/build/classes/kotlin/", excludes: fileFilter)
    def remoteSrc = "../network/src/main/java"

    getClassDirectories().setFrom(files(
            [appClassTree],
            [appKotlinClassTree],
            [presentationKotlinClassTree],
            [domainKotlinClassTree],
            [dataKotlinClassTree],
            [localKotlinClassTree],
            [remoteKotlinClassTree]
    ))
    getSourceDirectories().setFrom(files(
            [appSrc],
            [presentationSrc],
            [domainSrc],
            [dataSrc],
            [localSrc],
            [remoteSrc]
    ))

    executionData = fileTree(dir: project.buildDir, includes: ['jacoco/testDebugUnitTest.exec', 'outputs/code_coverage/debugAndroidTest/connected/**/*.ec'])
}